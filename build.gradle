/* (c) https://github.com/MontiCore/monticore */

plugins {
  id 'monticore' version '6.1.0' apply false
}

allprojects {
  group = 'montiarc'
  version = '6.0.0-SNAPSHOT'
  buildDir = "$projectDir/target"
}

description = """
MontiArc Version $version

Framework for Modeling and Simulation of Software Architectures

Developed at the Department of Software Engineering, RWTH Aachen University
http://www.se-rwth.de/"""

buildDir = "$projectDir/target"

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'
  apply plugin: 'idea'
  idea {
    module {
      testSourceDirs += file("$projectDir/target/generated-test-sources/monticore/sourcecode")
    }
  }

  repositories {
    mavenLocal()
    maven {
      credentials.username mavenUser
      credentials.password mavenPassword
      url repo
    }
    mavenCentral()
  }

  sourceCompatibility = '1.8'

  publishing {
    publications {
      maven(MavenPublication) {
        from(components.java)
      }
    }
    repositories.maven {
      credentials.username mavenUser
      credentials.password mavenPassword
      def releasesRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/montiarc-releases/"
      def snapshotsRepoUrl = "https://nexus.se.rwth-aachen.de/content/repositories/montiarc-snapshots/"
      url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
    }
  }

  tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
  }
}