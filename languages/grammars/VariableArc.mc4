/* (c) https://github.com/MontiCore/monticore */

/* This is a MontiCore stable grammar.
 * Adaptations -- if any -- are conservative. */

/**
 * This grammar defines the basic structural elements required for a variable
 * component architecture. The grammar contains definitions for if-statements,
 * constraints, and features.
 */
component grammar VariableArc extends ArcBasis {

  start ArcElement;

  /**
   * ASTFeatureDeclaration declares one or more features by specifying their name.
   *
   * @attribute features A list of declared feature identifiers.
   */
  ArcFeatureDeclaration implements ArcElement <110> =
    key("feature") (ArcFeature || ",")* ";"
  ;

  /**
   * ASTFeature defines the feature identifier, also creates corresponding symbols.
   *
   * @attribute name The name of the feature.
   */
  symbol ArcFeature = Name;

  /**
   * ArcIfStatement defines the variable elements of a component and the condition
   * for their inclusion. IfStatements of MC are not reused to keep component elements
   * and statements syntactically separated.
   *
   * @attribute condition The condition for which elements are added to the component.
   * @attribute thenStatement Elements added to the component if the condition evaluates to true.
   * @attribute elseStatement Elements added to the component if the condition evaluates to false.
   */
  ArcIfStatement implements ArcElement =
    "if" "(" condition:Expression ")" thenStatement:ArcElement
    ("else" elseStatement:ArcElement)?
  ;

  /**
   * ASTArcBlock holds multiple elements in a separate block.
   *
   * @attribute arcElements A list of architectural elements.
   */
  ArcBlock implements ArcElement = "{" ArcElement* "}" ;

  /**
   * ASTArcConstraintDeclaration defines rules for valid component instances.
   *
   * @attribute expression The constraint, which must be a valid boolean expression.
   */
  ArcConstraintDeclaration implements ArcElement <110> =
    "constraint" "(" Expression ")" ";"
  ;
}
