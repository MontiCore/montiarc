/* (c) https://github.com/MontiCore/monticore */

component grammar ArcAutomaton extends ArcBehaviorBasis,
            de.monticore.SCTransitions4Code {

  /**
   * Allows to specify the behavior of a component by using a statechart
   */
  ArcStatechart implements ArcBehaviorElement =
    "automaton" "{"
      SCStatechartElement*
    "}";

  /**
   * This element, when used, allows defining the initial output of an automaton.
   * Referencing a state here will mark the corresponding state as initial.
   */
  InitialOutputDeclaration implements SCStatechartElement =
    "initial" Name@SCState "/" SCABody ";";

  /**
   * This is an option to define entry and exit actions for a state
   */
  StateBody implements SCSBody = "{"
    SCStateElement*
  "}";

  /**
   * Define a possibility to enumerate ports
   */
  PortListEvent implements SCEvent = "<" (Name@Port || ",")* ">";

  // allow using statechart's keywords on other places as well
  nokeyword "initial", "final", "state";
  nokeyword "entry", "exit";

  splittoken "(...)";
}