/* (c) https://github.com/MontiCore/monticore */

plugins {
  id 'monticore'
}

group = "montiarc.languages"

def grammarDir = "src/main/grammars"

sourceSets {
  main {
    java.srcDirs += ["$buildDir/generated-sources/monticore/sourcecode"]
  }
  test {
    java.srcDirs += ["$buildDir/generated-test-sources/monticore/sourcecode"]
  }
}

test { useJUnitPlatform() }

configurations { grammar }

dependencies {
  //monticore dependencies
  implementation "de.monticore:monticore-generator:$monticore_version"
  implementation "de.monticore:monticore-runtime:$monticore_version"
  implementation "de.monticore:monticore-grammar:$monticore_version"
  grammar "de.monticore:monticore-grammar:$monticore_version:$grammar_classifier"
  testImplementation "de.monticore:monticore-runtime:$monticore_version:tests"
  testImplementation "de.monticore:monticore-runtime:$monticore_version"

  //internal dependencies
  implementation project(":util")
  implementation project(":languages:arc-fe")
  testImplementation project(":util").sourceSets.test.output
  testImplementation project(":util")

  //other dependencies
  implementation "de.se_rwth.commons:se-commons-logging:$se_commons_version"
  implementation "com.google.guava:guava:$guava_version"
  implementation "com.google.code.findbugs:jsr305:$jsr305_version"
  implementation "org.antlr:antlr4-runtime:$antlr_version"
  implementation "org.codehaus.janino:janino:$janino_version"
  testImplementation "org.mockito:mockito-core:$mockito_version"
  testImplementation "ch.qos.logback:logback-classic:$logback_version"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_jupiter_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
}

fileTree(grammarDir).matching { include '**/*.mc4' }.each {
  def g = it
  def grammarName = it.getName().substring(0, it.getName().lastIndexOf('.'))

  task "generateGrammar$grammarName"(type: MCTask) {
    modelPath(
      file("$projectDir/src/main/grammars"),
      file(project(":languages:arc-fe").projectDir.toURI().resolve("src/main/grammars")))
    grammar = file g
    outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
    handcodedPath file("$projectDir/src/main/java")
    def grammarIncludingPackage = file(grammarDir).toURI().relativize(g.toURI()).toString()
    def upToDate = incCheck(grammarIncludingPackage)
    outputs.upToDateWhen { upToDate }
  }
}

def grammarDependencies = ext {
  IfThenElse = [":languages:arc-fe:generateGrammarArcBasis"]
  MessageHandling = [":languages:arc-fe:generateGrammarArcBasis"]
  PrePostCondition = [":languages:arc-fe:generateGrammarArcBasis"]
  ValueRanges = [":languages:arc-fe:generateGrammarArcBasis"]
}

compileJava {
  tasks.findAll { task -> task.name.startsWith("generateGrammar") }.each {
    def grammarName = it.getName().substring('generateGrammar'.length())
    it.dependsOn grammarDependencies[grammarName]
    /*it.outputs.upToDateWhen {
      it.outputs.upToDateWhen && !it.dependsOn.any { !it.outputs.upToDateWhen }
    }*/
  }

  dependsOn project.collect { it.tasks.findAll { task -> task.name.startsWith("generateGrammar") } }
}