/* (c) https://github.com/MontiCore/monticore */

grammar MontiArc extends de.monticore.literals.MCCommonLiterals,
                         de.monticore.expressions.CommonExpressions,
                         de.monticore.expressions.AssignmentExpressions,
                         de.monticore.types.MCSimpleGenericTypes,
                         de.monticore.statements.MCCommonStatements,
                         de.monticore.statements.MCVarDeclarationStatements,
                         ArcBasis, StatechartBasis, ComfortableArc,
                         GenericArc {

  /** ASTMACompilationUnit represents the entire content of a MontiArc model
   * @attribute package The package declaration of the component model
   * @attribute importStatement List of imported elements
   * @attribute component Outermost component defined in this compilation unit.
   */
  scope MACompilationUnit =
    ("package" package:MCQualifiedName ";")?
    importStatement:MCImportStatement*
    ComponentType
    EOF
  ;

  /**
   * ASTArcBehavior is the top-level interface for component behavior embedding.
   */
  interface ArcBehavior extends ArcElement;

  /**
   * ASTStatechartBehavior embedds statecharts for component behavior
   * specification.
   */
  StatechartBehavior implements ArcBehavior = Statechart;

  /**
   * ASTArcTiming sets the timing of a component.
   */
  ArcTiming implements ArcElement  =
    key("timing") ArcTimeMode
  ;

  interface ArcTimeMode;

  ArcInstant implements ArcTimeMode = key("instant");

  ArcDelayed implements ArcTimeMode = key("delayed");

  ArcSync implements ArcTimeMode = key("sync");

  ArcCausalSync implements ArcTimeMode = key("causalsync");

  ArcUntimed implements ArcTimeMode = key("untimed");
}