import java.nio.file.Paths

/* (c) https://github.com/MontiCore/monticore */

plugins {
  id "monticore"
  id "jacoco"
  id "com.github.johnrengelman.shadow" version "$shadow_version"

}

group = "montiarc.languages"

def grammarDir = "$projectDir/src/main/grammars"
def grammarOutDir = "$buildDir/generated-sources/monticore/sourcecode"

sourceSets {
  main {
    java.srcDirs += ["$grammarOutDir"]
  }
  grammars {
    resources {
      srcDirs(grammarDir)
      include "**/*.mc4"
    }
  }
}

jacocoTestReport.configure {
  // We only want to analyze code coverage based on our hwc source code, not the one generated from our grammars
  def hwcPath = "$projectDir/src/main/java"
  def classesToReport = fileTree(hwcPath).collect {
    def packagePath = Paths.get(hwcPath).relativize(it.toPath())
    def packagePathWithClassEnding = packagePath.toString().replace(".java", ".class")
    def classLocation = Paths.get("$buildDir/classes/java/main").resolve(packagePathWithClassEnding)
    classLocation
  }
  classDirectories.setFrom files([classesToReport, "$buildDir/classes/resources/main"])
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

configurations {
  runtime {
    exclude group: "de.monticore.lang", module: "cd4analysis"
    exclude group: "org.codehaus.groovy", module: "groovy"
  }
  grammar
}

dependencies {
  //Grammar dependencies
  grammar ("de.monticore:monticore-grammar:$mc_version") {
    capabilities {
      requireCapability("de.monticore:monticore-grammar-grammars")
    }
  }
  grammar("de.monticore.lang:statecharts:$mc_version") {
    capabilities {
      requireCapability("de.monticore.lang:statecharts-$grammars_classifier")
    }
  }
  grammar(project(":languages:arc-fe")) {
    capabilities {
      requireCapability("montiarc.languages:arc-fe-$grammars_classifier")
    }
  }

  //MontiCore dependencies
  implementation("de.monticore.lang:statecharts:$mc_version")
  implementation("de.monticore:monticore-runtime:$mc_version")
  implementation("de.monticore:monticore-grammar:$mc_version")
  implementation("de.monticore:class2mc:$mc_version")
  testImplementation("de.monticore:monticore-runtime:$mc_version") {
    capabilities {
      requireCapability("de.monticore:monticore-runtime-tests")
    }
  }
  testImplementation("de.monticore:monticore-runtime:$mc_version")

  //Internal dependencies
  implementation(project(":languages:arc-fe"))
  testImplementation(project(":languages:arc-fe").sourceSets.test.output)

  //Other dependencies
  implementation("de.se_rwth.commons:se-commons-logging:$se_commons_version")
  implementation("com.google.guava:guava:$guava_version")
  implementation("org.antlr:antlr4-runtime:$antlr_version")
  implementation("org.codehaus.janino:janino:$janino_version")
  testImplementation("org.mockito:mockito-core:$mockito_version")
  testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version")
  testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_jupiter_version")
  testImplementation("org.assertj:assertj-core:$assertj_version")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version")
}

task generate {}

// one task per grammar file
fileTree(grammarDir).matching { include '**/*.mc4' }.each { File g ->
  String taskname = "generateGrammar${g.getName().substring(0, g.getName().lastIndexOf('.'))}"
  task "$taskname"(type: MCTask) {

    grammar = g
    outputDir = file grammarOutDir
    def grammarIncludingPackage = file(grammarDir).toURI().relativize(g.toURI()).toString()
    outputs.upToDateWhen { incCheck(grammarIncludingPackage) }
  }
  generate.dependsOn("$taskname")
}

compileJava.dependsOn(generate)

java {
  //withJavadocJar()
  withSourcesJar()
  registerFeature("$grammars_classifier") {
    usingSourceSet(sourceSets.grammars)
  }
}

// all in one tool-jar
shadowJar {
  manifest {
    attributes "Main-Class": "montiarc.MontiArcTool"
  }
  archiveClassifier = "mc-tool"
  archiveBaseName = "MontiArc"
  archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

jar.dependsOn(shadowJar)
jar.dependsOn(grammarsJar)
compileJava.mustRunAfter(project.collect {it.project(":languages:arc-fe").getTasks().withType(Test) })

publishing {
  publications {
    mavenJava(MavenPublication) { from(components.java) }
  }
  repositories.maven {
    name = "$SE_Nexus_ID"
    credentials.username(mavenUser)
    credentials.password(mavenPassword)
    url = version.endsWith('SNAPSHOT') ? "$snapshotsRepoUrl" : "$releasesRepoUrl"
  }
}