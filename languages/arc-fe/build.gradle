/* (c) https://github.com/MontiCore/monticore */

plugins {
  id "monticore"
  id "jacoco"
}

group = "montiarc.languages"

def grammarDir = "$projectDir/src/main/grammars"
def reportsDir = "$buildDir/generated-sources/monticore/sourcecode/reports/"

sourceSets {
  main {
    java.srcDirs += ["$buildDir/generated-sources/monticore/sourcecode"]
  }
  test {
    java.srcDirs += ["$buildDir/generated-test-sources/monticore/sourcecode"]
  }
  grammars {
    resources {
      srcDirs(grammarDir, reportsDir)
      include("**/*.mc4", "**/*.cd")
    }
  }
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport
}

configurations { grammar }

dependencies {
  //monticore dependencies
  implementation "de.monticore:monticore-generator:$mc_version"
  implementation "de.monticore:monticore-runtime:$mc_version"
  implementation "de.monticore:monticore-grammar:$mc_version"
  implementation "de.monticore:monticore-grammar:$mc_version:$grammar_classifier"
  grammar "de.monticore:monticore-grammar:$mc_version:$grammar_classifier"
  testImplementation "de.monticore:monticore-runtime:$mc_version:tests"
  testImplementation "de.monticore:monticore-runtime:$mc_version"

  //internal dependencies
  implementation project(":util")
  testImplementation project(":util").sourceSets.test.output
  testImplementation project(":languages:montiarc-fe")

  //other dependencies
  implementation "de.se_rwth.commons:se-commons-logging:$se_commons_version"
  implementation "com.google.guava:guava:$guava_version"
  implementation "com.google.code.findbugs:jsr305:$jsr305_version"
  implementation "org.antlr:antlr4-runtime:$antlr_version"
  implementation "org.codehaus.janino:janino:$janino_version"
  testImplementation "org.mockito:mockito-core:3.2.0"
  testImplementation "ch.qos.logback:logback-classic:1.1.2"
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
  testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_jupiter_version"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
}

fileTree(grammarDir).matching { include '**/*.mc4' }.each {
  def g = it
  def grammarName = it.getName().substring(0, it.getName().lastIndexOf('.'))

  task "generateGrammar$grammarName"(type: MCTask) {
    grammar = file g
    outputDir = file "$buildDir/generated-sources/monticore/sourcecode"
    handcodedPath file("$projectDir/src/main/java")
    def grammarIncludingPackage = file(grammarDir).toURI().relativize(g.toURI()).toString()
    def upToDate = incCheck(grammarIncludingPackage)
    outputs.upToDateWhen { upToDate }
  }
}

def grammarDependencies = ext {
  ArcBasis = []
  ComfortableArc = [":languages:arc-fe:generateGrammarArcBasis"]
  GenericArc = [":languages:arc-fe:generateGrammarArcBasis"]
  ArcCore = [":languages:arc-fe:generateGrammarArcBasis",
             ":languages:arc-fe:generateGrammarComfortableArc",
             ":languages:arc-fe:generateGrammarGenericArc"]
}

compileJava {
  tasks.findAll { task -> task.name.startsWith("generateGrammar") }.each {
    def grammarName = it.getName().substring('generateGrammar'.length())
    it.dependsOn grammarDependencies[grammarName]
    /*it.outputs.upToDateWhen {
      it.outputs.upToDateWhen && !it.dependsOn.any { !it.outputs.upToDateWhen }
    }*/
  }

  dependsOn project.collect { it.tasks.findAll { task -> task.name.startsWith("generateGrammar") } }
}

java {
  //withJavadocJar()
  withSourcesJar()
  registerFeature("grammars") {
    usingSourceSet(sourceSets.grammars)
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) { from components.java }
  }
  repositories.maven {
    name = "$SE_Nexus_ID"
    credentials.username mavenUser
    credentials.password mavenPassword
    url = version.endsWith('SNAPSHOT') ? "$snapshotsRepoUrl" : "$releasesRepoUrl"
  }
}