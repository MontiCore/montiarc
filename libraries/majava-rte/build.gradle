/* (c) https://github.com/MontiCore/monticore */

plugins {
  id "com.github.johnrengelman.shadow" version "$shadow_version"
}

group = 'montiarc.libraries'

java {
  //withJavadocJar()
  withSourcesJar()
}

shadowJar {
  minimize()

  zip64 = true
  archiveClassifier = "library"
  archiveBaseName = "maJava-rte"
  archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

jar.dependsOn(shadowJar)

publishing {

  publications {
    mavenJava(MavenPublication) { from(components.java) }
  }

  repositories {

    maven {
      name = "$SE_Nexus_ID"
      credentials.username(mavenUser)
      credentials.password(mavenPassword)
      url = version.endsWith('SNAPSHOT') ? "$snapshotsRepoUrl" : "$releasesRepoUrl"
    }

    def CI_JOB_TOKEN = System.getenv("CI_JOB_TOKEN")
    if(CI_JOB_TOKEN != null) {
      maven {
        name = "MontiArcGitlab"
        url = montiArcGitLabMaven
        credentials(HttpHeaderCredentials) {
          name = "Job-Token"
          value = CI_JOB_TOKEN
        }
        authentication {
          header(HttpHeaderAuthentication)
        }
      }
    }
  }
}