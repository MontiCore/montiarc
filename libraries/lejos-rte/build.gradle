/* (c) https://github.com/MontiCore/monticore */

group = "montiarc.libraries"

def hwcDir = "$projectDir/src/main/java"
def genDir = "$buildDir/generated-sources"

sourceSets {
  main {
    java.srcDirs += ["$hwcDir", "$genDir"]
  }
}

configurations {
  generateCD
  generateMA
}

dependencies {
  //MontiCore dependencies
  implementation("de.se_rwth.commons:se-commons-logging:$se_commons_version")

  //Internal dependencies
  generateCD(project(":generators:cd2pojo"))
  generateMA(project(":generators:ma2java"))
  implementation(project(":libraries:majava-rte"))

  //Other dependencies
  implementation("lejos.nxt:classes:$lejos_version")
  testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version")
  testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_jupiter_version")
  testImplementation("org.mockito:mockito-core:$mockito_version")
}

task generateCD(type: JavaExec) {
  classpath = configurations.generateCD
  mainClass = "de.monticore.cd2pojo.POJOGeneratorScript"

  args("$projectDir/src/main/resources", "$genDir", "$hwcDir")
}

task generateMontiArc(type: JavaExec) {
  classpath = configurations.generateMA
  mainClass = "montiarc.generator.MontiArcGeneratorTool"

  args("$projectDir/src/main/resources", "$genDir", "$hwcDir")
}

generateMontiArc.dependsOn(generateCD)

task librarymodelsJar(type: Jar) {
  from("$projectDir/src/main/resources").collect()
  archiveClassifier = "$librarymodels_classifier"
}

compileJava.dependsOn(generateMontiArc)
librarymodelsJar.mustRunAfter(generateMontiArc)

publishing {
  publications {
    mavenJava(MavenPublication) {
      from(components.java)
      artifact librarymodelsJar
    }
  }
  repositories.maven {
    name = "$SE_Nexus_ID"
    credentials.username(mavenUser)
    credentials.password(mavenPassword)
    url = version.endsWith('SNAPSHOT') ? "$snapshotsRepoUrl" : "$releasesRepoUrl"
  }
}